apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'jacoco-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.0.0-rc02'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'

    // Junit5
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit5_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5_version"

    // Tests - Spek
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Mockito
    testImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'com.guhungry.android:extensions'

    publishedGroupId = 'com.guhungry.android'
    libraryName = 'AndroidExtensions'
    artifact = 'extensions'

    libraryDescription = 'Kotlin extensions make android development sweeter'

    siteUrl = 'https://github.com/guhungry/android-extensions'
    gitUrl = 'https://github.com/guhungry/android-extensions.git'

    libraryVersion = android.defaultConfig.versionName

    developerId = 'guhungry1'
    developerName = 'Woraphot Chokratanasombat'
    developerEmail = 'Woraphot Chokratanasombat'

    licenseName = 'MIT'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

apply from: "$rootDir/gradle/jcenter.gradle"

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.property('bintrayUser')
    bintray.key = project.property('bintrayKey')

    println project.property('bintrayUser')
    println project.property('bintrayKey')
} else if (project.rootProject.file('local.properties').exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray.user = properties.getProperty("bintrayUser")
    bintray.key = properties.getProperty("bintrayKey")
}

apply plugin: 'org.sonarqube'